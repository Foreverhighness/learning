LinkerScript = {
    // SOI ~ OutputFormat? ~ OutputArch ~ Entry ~ Statement* ~ Sections+ ~ EOI
    SOI ~ Command* ~ EOI
}

COMMENT    = _{ "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
WHITESPACE = _{ " " | "\t" | NEWLINE }

Command      = _{ Macro | OutputFormat | OutputArch | Entry | Assertion | Phdrs | Sections | SymbolAssignment }
OutputFormat =  { "OUTPUT_FORMAT(" ~ (#format = bfdname | (#default = bfdname ~ "," ~ #big = bfdname ~ "," ~ #little = bfdname)) ~ ")" }
OutputArch   =  { "OUTPUT_ARCH(" ~ #arch = bfdarch ~ ")" }
Entry        =  { "ENTRY(" ~ #entry = symbol ~ ")" }
Assertion    =  { "ASSERT(" ~ expr ~ "," ~ string ~ ")" }
// <https://sourceware.org/binutils/docs/ld/Miscellaneous-Commands.html>

/// PHDRS {
/// name type [ FILEHDR ] [ PHDRS ] [ AT ( address ) ]
/// [ FLAGS ( flags ) ] ;
/// }
Phdrs           = {
    "PHDRS" ~ "{" //
  // TODO: ~ FILEHDR? ~  PHDRS?  ~ AtAddress? ~ FLAGS ( flags )
  ~ PhdrDescription* //
  ~ "}"
}
PhdrDescription = { #name = ident ~ #ty = ident ~ ("FLAGS" ~ "(" ~ #flags = integer ~ ")")? ~ ";" }

/// The format of the SECTIONS command is:
/// SECTIONS
/// {
/// sections-command
/// sections-command
/// …
/// }
Sections        =  { "SECTIONS" ~ "{" ~ SectionsCommand* ~ "}" }
SectionsCommand = _{ Macro | Entry | SymbolAssignment | Assertion | OutputSectionDescription }

AtAddress = { "AT(" ~ expr ~ ")" }

/// https://sourceware.org/binutils/docs/ld/Output-Section-Description.html
/// section [address] [(type)] :
/// [AT(lma)]
/// [ALIGN(section_align) | ALIGN_WITH_INPUT]
/// [SUBALIGN(subsection_align)]
/// [constraint]
/// {
/// output-section-command
/// output-section-command
/// …
/// } [>region] [AT>lma_region] [:phdr :phdr …] [=fillexp] [,]
OutputSectionDescription = {
    #section = section ~ #address = expr? ~ #ty = ("(" ~ ident ~ ")")? ~ ":" //
  ~ #at = AtAddress? //
  ~ #align = align? //
  ~ "{" //
  ~ #commands = OutputSectionCommand* //
  ~ "}" ~ #phdrs = OutputSectionPhdr* ~ #fillexp = ("=" ~ constant)?
}
OutputSectionPhdr        = { ":" ~ ident }

/// Each output-section-command may be one of the following:
/// - a symbol assignment
/// - an input section description
/// - data values to include directly
/// - a special output section keyword
OutputSectionCommand = _{
    Macro
  | SymbolAssignment
  | OutputSectionData
  | InputSectionDescription
}

FilenameWildcard        = _{ "*" | #filename = ident }
SectionWildcard         = _{ section ~ "*"? }
SectionWildcardSort     = _{ "SORT(" ~ SectionWildcard ~ ")" }
InputSection            = _{ FilenameWildcard ~ "(" ~ (SectionWildcardSort | SectionWildcard)+ ~ ")" }
InputSectionDescription =  { ("KEEP(" ~ InputSection+ ~ ")") | InputSection ~ ";"? }
OutputSectionData       =  { Byte }
Byte                    =  { "BYTE(" ~ expr ~ ")" ~ ";" }

SymbolAssignment = { symbol ~ assign_op ~ expr ~ ";" }
assign_op        = { ("+=" | "=") }
// Unless quoted, symbol names start with a letter, underscore, or period and may include letters, digits, underscores, periods, and hyphens.
ident  = @{ ("_" | "." | ASCII_ALPHA) ~ ("_" | "-" | "." | "$" | ASCII_ALPHANUMERIC)+ }
symbol = @{ "." | ident }

atom    =  { constant | builtin_fn | symbol }
primary = _{ Assertion | atom | "(" ~ expr ~ ")" }
// https://docs.rs/pest/latest/pest/pratt_parser/struct.PrattParser.html
expr = { unary_op* ~ primary ~ (binary_op ~ unary_op* ~ primary)* }
// infix
// binary_op   = { "+" | "-" | "*" | "&" | "==" | "<=" | "||" | "<<" | ">>" }
binary_op   = _{ add | sub | mul | bitwise_and | eq | le | logical_or | left_shift | right_shift }
add         =  { "+" }
sub         =  { "-" }
mul         =  { "*" }
bitwise_and =  { "&" }
eq          =  { "==" }
le          =  { "<=" }
logical_or  =  { "||" }
left_shift  =  { "<<" }
right_shift =  { ">>" }
// prefix
// unary_op = { "-" | "~" }
unary_op    = _{ neg | bitwise_not }
neg         =  { "-" }
bitwise_not =  { "~" }
decimal     = @{ ASCII_DIGIT+ }
octal       = @{ "0" ~ ASCII_OCT_DIGIT+ }
hexadecimal = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT+ }
integer     =  { hexadecimal | octal | decimal }
// the linker considers an integer beginning with ` 0 ' to be octal, and an integer beginning with ` 0x ' or ` 0X ' to be hexadecimal.
// The linker considers other integers to be decimal.
// In addition, you can use the suffixes ` K ' and ` M ' to scale a constant by ` 1024 ' or ` 1024*1024 ' respectively.
constant   = @{ integer ~ ("K" | "M")? }
builtin_fn = _{ absolute | addr | align | defined | sizeof }
absolute   = _{ "ABSOLUTE(" ~ expr ~ ")" }
addr       = _{ "ADDR(" ~ section ~ ")" }
align      = _{ "ALIGN(" ~ expr ~ ("," ~ expr)? ~ ")" }
defined    = _{ "DEFINED(" ~ symbol ~ ")" }
sizeof     = _{ "SIZEOF(" ~ section ~ ")" }
section    = @{ ("/" | "_" | "." | "-" | "+" | ASCII_ALPHANUMERIC)+ }

Macro          =  { ARM_MACROS | OF_TABLE | CONSTRUCTIONRS }
OF_TABLE       = _{ "_OF_TABLE_IS_ENABLED(" ~ (!")" ~ ANY)* ~ ")" ~ "(" ~ (!")" ~ ANY)* ~ ")" }
CONSTRUCTIONRS = _{ "CONSTRUCTORS" }
ARM_MACROS     = _{ ARM_KEEP | ARM_MACRO | HEAD_SYMBOLS }
ARM_MACRO      = @{ "ARM_" ~ ASCII_ALPHA_UPPER+ }
ARM_KEEP       = _{ ARM_MACRO ~ "_KEEP(" ~ InputSectionDescription+ ~ ")" }
HEAD_SYMBOLS   = _{ "HEAD_SYMBOLS" }

bfdname = @{ ("_" | "-" | ":" | ASCII_ALPHANUMERIC)+ }
bfdarch =  { bfdname }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
